
<!doctype html>
<html lang="ja">
<head>
	<meta charset="UTF-8">
	<title>akrantiain2 parser js</title>
	
	<style>
.error {
	color: red;
}
pre {
	border: #888 solid 1px;
	background-color: #ddd;
	padding: 0.5em;
}

footer {
    font-size: 70%;
}
	</style>


</head>
<body>

	<h2>akrantiain2 parser js (beta)</h2>
	<p>
		<a href="https://github.com/sozysozbot/akrantiain2">akrantiain2 by la. sozysozbot.</a>
	</p>

	
	<h3>PEG.js</h3>
	<pre>
Sentences = xs:(Sentence / Comment / Space)* {
    var ret = {
        define: {},
        conversions: [],
        option: {}
    };
    xs.forEach(function(x) {
        if (x === null ) return;
        if (x.define)
            ret.define[x.define.id] = x.define.values;
        if (x.conversion)
            ret.conversions.push(x.conversion);
        if (x.option)
            ret.option[x.option] = true;
    });
    return ret;
}

Sentence = sent:(Conversion / Define / AtSignOption) _ ';'? { return sent; }

Comment = '#' [^\n]* '\n'? { return null; }
Space = [ \t\r\n\f\v] { return null; }

String = '"' chars:([^\\"\n] / EscapeSequence)* '"' { return chars.join(''); }
Slash = '/' chars:([^\\/\n] / EscapeSequence)* '/' { return chars.join(''); }
EscapeSequence = '\\' char:('\\' / '"' / '/' / "'") { return char; }

Identifier = $([A-Za-z] [A-Za-z0-9_]*)

Define = id:Identifier _ '=' _ values:StringsSepByPipe {
    return { define: {
        id: id,
        values: values.map(function(x) { return x.join(""); })
    }};
}

Select
    = '^' { return { bound: true }; }
    / id:Identifier { return { id: id }; }
    / Strings
    / '(' xs:StringsSepByPipe ')' {
        return { or: xs.map(function(x) { return x.join(""); }) };
    }
Strings = head:String tail:(_ String)* {
    return [head].concat(tail.map(function(x) { return x[1]; }));
}
StringsSepByPipe = head:Strings tail:(_ '|' _ Strings)* {
    return [head].concat(tail.map(function(x) { return x[3]; }));
}

Conversion = lcond:NegSelect? _ selects:Selects _ rcond:NegSelect? _
        '->' _ phonemes:Phonemes {
    if (lcond != null) { selects.unshift(lcond); }
    if (rcond != null) { selects.push(rcond); }
    return { conversion: {
        selects: selects,
        phonemes: phonemes
    }};
}
NegSelect = '!' _ select:Select { return { not: select }; }
Selects = head:Select tail:(_ Select)* {
    var xs = [head].concat(tail.map(function(x) { return x[1]; }));
    var ret= [];
    xs.forEach(function(x) {
        if (Array.isArray(x))
            ret = ret.concat(x);
        else
            ret.push(x);
    });
    return ret;
}

Phonemes = head:Phoneme tail:(_ Phoneme)* {
    return [head].concat(tail.map(function(x) { return x[1]; }));
}
Phoneme = '$' { return null; } / Slash

AtSignOption = '@' _ id:Identifier {
    return { option: id };
}

_ = [ \t\r\f\v]*
	</pre>
 <footer>
        The parser was <a href="http://www53.atpages.jp/~titlil/temk/akrantiain2/parser.html">originally written</a> by <a href="https://twitter.com/na2co3_ftw">@na2co3_ftw</a>, forked here thanks to the <a href="https://twitter.com/na2co3_ftw/status/889713684376723456">explicit</a> <a href="https://twitter.com/na2co3_ftw/status/889714229548171264">permissions</a> by the author.
</footer>

</body>
</html>
